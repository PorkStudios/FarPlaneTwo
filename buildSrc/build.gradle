/*
 * Adapted from The MIT License (MIT)
 *
 * Copyright (c) 2020-2022 DaPorkchop_
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation
 * files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy,
 * modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software
 * is furnished to do so, subject to the following conditions:
 *
 * Any persons and/or organizations using this software must include the above copyright notice and this permission notice,
 * provide sufficient credit to the original authors of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
 * OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
 * BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

plugins {
    id "com.github.johnrengelman.shadow" version "$shadowPluginVersion"
}

allprojects {
    apply plugin: "java"

    repositories {
        mavenCentral()
        maven {
            name = "DaPorkchop_"
            url = "https://maven.daporkchop.net/"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }
}

apply plugin: "java-gradle-plugin"

configurations {
    shade
    compileOnly.extendsFrom shade
}

dependencies {
    implementation gradleApi()
    shade project(":resources")

    shade "net.daporkchop.lib:common:$porklibVersion"

    shade "com.google.code.gson:gson:$gsonVersion"
}

gradlePlugin {
    plugins {
        register("fp2") {
            id = "fp2"
            implementationClass = "net.daporkchop.fp2.gradle.FP2GradlePlugin"
        }
    }
}

//create shaded jar and reobfuscate it
shadowJar {
    archiveClassifier = null
    configurations = [project.configurations.shade]

    exclude 'module-info.class'

    relocate("net.daporkchop.lib", "net.daporkchop.fp2.buildSrc.deps.porklib")
    relocate("com.google.gson", "net.daporkchop.fp2.buildSrc.deps.gson")
}

jar.enabled = false
build.dependsOn shadowJar